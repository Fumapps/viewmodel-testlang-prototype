ext.mpsVersion = "2021.2.4"
ext.mpsPlugins = [ "de.itemis.mps.extensions.build": 17136,
                   "de.itemis.mps.grammarcells": 13242 ]

apply from: 'gradle/download-mps.gradle'

task prepareMps {
    group 'mps'
    dependsOn buildOsSpecificBundle
}

import org.apache.tools.ant.taskdefs.condition.Os
task openProjectInMps(type: Exec) {
    group 'mps'
    dependsOn prepareMps

    workingDir projectDir

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'build/mps-bundle/mps/bin/mps.bat', projectDir
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'build/mps-bundle/Contents/MacOS/mps', projectDir
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        commandLine 'sh', 'build/mps-bundle/mps/bin/mps.sh', projectDir
    }
}

ant.importBuild("build.xml") { antTargetName ->
    'ant-' + antTargetName
}
ant.properties.'mps_home' = getMpsHome()

task mpsBuildXml {
    group 'mps'
    dependsOn 'ant-build'
}

private static String getMpsHome() {
    if (Os.isFamily(Os.FAMILY_MAC)) {
        return 'build/mps-bundle/Contents'
    }
    return 'build/mps-bundle/mps'
}
