
def hamsterSubDir = file("build/hamster")
def hamsterWorkingDir = file("$hamsterSubDir/code")

task downloadHamsterSimulator() {
    group 'hamster'

    onlyIf {
        !hamsterWorkingDir.exists()
    }

    doLast {
        def url = 'https://github.com/SQAHamster/mpw-hamster-examples/archive/refs/heads/master.zip'
        println("downloading hamstersimulator from $url")
        def zipFile = file("$hamsterSubDir/repo-master.zip")

        if (zipFile.exists()) {
            zipFile.delete()
        }

        zipFile.parentFile.mkdirs()
        new URL(url).withInputStream { downloadInputStream -> zipFile.withOutputStream { it << downloadInputStream } }

        copy {
            from zipTree(zipFile).matching { include '**/java-examples/**/*' }
            into hamsterSubDir
        }

        file("$hamsterSubDir/mpw-hamster-examples-master/java-examples").renameTo(hamsterWorkingDir)
        file("$hamsterSubDir/mpw-hamster-examples-master").deleteDir()
    }

}

import org.apache.tools.ant.taskdefs.condition.Os
task makeAndTestHamsterSimulatorViewTest(type: Exec) {
    group 'hamster'
    dependsOn downloadHamsterSimulator

    // modify the pom.xml to integrate junit (this shall be done directly in the hamster-examples repo in future)
    doFirst {
        def pomFile = file("$hamsterWorkingDir/pom.xml")
        if (!pomFile.text.contains("org.junit.jupiter")) {
            pomFile.text = pomFile.text.replace("</dependencies>", """\t<dependency>
\t\t\t<groupId>org.junit.jupiter</groupId>
\t\t\t<artifactId>junit-jupiter</artifactId>
\t\t\t<version>5.7.1</version>
\t\t\t<scope>test</scope>
\t\t</dependency>
\t</dependencies>
""")
        }

        checkIfGeneratedViewTestsExist(hamsterWorkingDir)
        getRidOfModuleInfo(hamsterWorkingDir)
        createExampleTerritory(hamsterWorkingDir)
    }

    if (System.getenv("JAVA_HOME_15") != null) {
        environment "JAVA_HOME": System.getenv("JAVA_HOME_15")
    }

    workingDir hamsterWorkingDir
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'mvn.cmd', 'test'
    } else {
        commandLine 'mvn', 'test'
    }
}

private void checkIfGeneratedViewTestsExist(File hamsterWorkingDir) {
    def testFolder = file("$hamsterWorkingDir/src/test/java/gentests")
    if (!testFolder.exists() || testFolder.listFiles().size() == 0) {
        throw new GradleException("Illegal call of 'makeAndTestHamsterSimulatorViewTest': no hamster view tests are generated")
    }
}

/*
  The generated tests shall use the HamsterViewPresenter class for convenience reasons. To get access,
  disable the module-visibility feature by deleting the module-info.java file.
 */
private void getRidOfModuleInfo(File hamsterWorkingDir) {
    def moduleInfoFile = file("$hamsterWorkingDir/src/main/java/module-info.java")
    moduleInfoFile.delete()
}

private void createExampleTerritory(File hamsterWorkingDir) {
    def territoryFile = file("$hamsterWorkingDir/src/main/resources/example.ter")
    if (!territoryFile.exists()) {
        territoryFile.text = """
3
3
###
> *
###
0
0
1
""".trim()
    }
}
